# Copyright (C) 2021-2022 HiHope Open Source Organization .
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

group("display_group") {
  deps = [
    ":display_device",
    ":display_gfx",
    ":display_gralloc",
    ":higbm",
  ]
}

group("display_buffer_hwi_group") {
  deps = [
    ":libdisplay_buffer_hwi_impl",
    ":libdisplay_buffer_vendor",
  ]
}

group("display_composer_hwi_group") {
  deps = [
    ":libdisplay_composer_hwi_impl",
    ":libdisplay_composer_vendor",
  ]
}

ohos_prebuilt_shared_library("libdisplay_buffer_hwi_impl") {
  if (target_cpu == "arm") {
    source = "lib/libdisplay_buffer_hwi_impl.z.so"
  } else {
    source = "lib64/libdisplay_buffer_hwi_impl.z.so"
  }
  install_enable = true
  install_images = [ chipset_base_dir ]
  subsystem_name = "hdf"
  part_name = "drivers_peripheral_display_buffer"
}

ohos_prebuilt_shared_library("libdisplay_buffer_vendor") {
  if (target_cpu == "arm") {
    source = "lib/libdisplay_buffer_vendor.z.so"
  } else {
    source = "lib64/libdisplay_buffer_vendor.z.so"
  }
  install_enable = true
  install_images = [ chipset_base_dir ]
  subsystem_name = "hdf"
  part_name = "drivers_peripheral_display_buffer"
}

ohos_prebuilt_shared_library("libdisplay_composer_hwi_impl") {
  if (target_cpu == "arm") {
    source = "lib/libdisplay_composer_hwi_impl.z.so"
  } else {
    source = "lib64/libdisplay_composer_hwi_impl.z.so"
  }
  install_enable = true
  install_images = [ chipset_base_dir ]
  subsystem_name = "hdf"
  part_name = "drivers_peripheral_display_composer"
}

ohos_prebuilt_shared_library("libdisplay_composer_vendor") {
  if (target_cpu == "arm") {
    source = "lib/libdisplay_composer_vendor.z.so"
  } else {
    source = "lib64/libdisplay_composer_vendor.z.so"
  }
  install_enable = true
  install_images = [ chipset_base_dir ]
  subsystem_name = "hdf"
  part_name = "drivers_peripheral_display_composer"
}

ohos_static_library("higbm") {
  sources = [ "src/display_gralloc/hi_gbm.c" ]
  include_dirs = [
    "include",
    "//commonlibrary/c_utils/base/include",
    "//base/hiviewdfx/interfaces/innerkits/libhilog/include",
  ]
  output_name = "higbm"
  cflags = [
    "-DGRALLOC_GBM_SUPPORT",
    "-Wno-macro-redefined",
  ]
  deps = [ "//third_party/libdrm:libdrm" ]
  external_deps = [
    "c_utils:utils",
    "hiviewdfx_hilog_native:libhilog",
  ]
}

ohos_shared_library("display_gralloc") {
  sources = [
    "src/display_gralloc/display_gralloc.c",
    "src/display_gralloc/display_gralloc_gbm.c",
  ]
  include_dirs = [
    "include",
    "//drivers/peripheral/display/interfaces/include",
    "//drivers/peripheral/base",
    "//drivers/hdf_core/framework/include/utils",
    "//drivers/hdf_core/adapter/uhdf2/osal/include",
    "//commonlibrary/c_utils/base/include",
    "//foundation/graphic/standard/utils/include",
    "//foundation/graphic/standard/prebuilts/librarys/drm/include",
    "//base/hiviewdfx/interfaces/innerkits/libhilog/include",
  ]
  output_name = "display_gralloc"
  cflags = [
    "-DGRALLOC_GBM_SUPPORT",
    "-Wno-macro-redefined",
  ]
  deps = [
    ":higbm",
    "//third_party/libdrm:libdrm",
  ]

  external_deps = [
    "c_utils:utils",
    "hiviewdfx_hilog_native:libhilog",
  ]

  install_enable = true
  install_images = [ chipset_base_dir ]
  subsystem_name = "hdf"
  part_name = "drivers_peripheral_display"
}

ohos_shared_library("display_gfx") {
  sources = [ "src/display_gfx/display_gfx.c" ]
  include_dirs = [
    "include",
    "//drivers/peripheral/display/interfaces/include",
    "//drivers/peripheral/base",
    "//drivers/hdf_core/framework/include/utils",
    "//drivers/hdf_core/adapter/uhdf2/adapter/osal/include/",
    "//commonlibrary/c_utils/base/include",
    "//foundation/graphic/standard/utils/include",
    "//device/soc/rockchip/rk3568/hardware/rga/include/",
    "//base/hiviewdfx/interfaces/innerkits/libhilog/include",
  ]
  output_name = "display_gfx"
  cflags = [ "-Wno-macro-redefined" ]
  deps = [
    ":display_gralloc",
    "//device/soc/rockchip/rk3568/hardware/rga:librga",
  ]

  external_deps = [
    "c_utils:utils",
    "hiviewdfx_hilog_native:libhilog",
  ]

  install_enable = true
  install_images = [ chipset_base_dir ]
  subsystem_name = "hdf"
  part_name = "drivers_peripheral_display"
}

ohos_shared_library("display_device") {
  sources = [
    "src/display_device/drm_connector.cpp",
    "src/display_device/drm_crtc.cpp",
    "src/display_device/drm_device.cpp",
    "src/display_device/drm_display.cpp",
    "src/display_device/drm_encoder.cpp",
    "src/display_device/drm_plane.cpp",
    "src/display_device/drm_vsync_worker.cpp",
    "src/display_device/hdi_composer.cpp",
    "src/display_device/hdi_device_interface.cpp",
    "src/display_device/hdi_display.cpp",
    "src/display_device/hdi_drm_composition.cpp",
    "src/display_device/hdi_drm_layer.cpp",
    "src/display_device/hdi_gfx_composition.cpp",
    "src/display_device/hdi_layer.cpp",
    "src/display_device/hdi_netlink_monitor.cpp",
    "src/display_device/hdi_session.cpp",
  ]
  output_name = "display_device"
  include_dirs = [
    "src/display_device",
    "include",
    "//drivers/peripheral/display/interfaces/include",
    "//drivers/peripheral/base",
    "//drivers/hdf_core/framework/include/utils",
    "//drivers/hdf_core/adapter/uhdf2/osal/include",
    "//commonlibrary/c_utils/base/include",
    "//foundation/graphic/standard/utils/include",
    "//foundation/graphic/standard/prebuilts/librarys/drm/include",
    "//base/hiviewdfx/interfaces/innerkits/libhilog/include",
  ]
  deps = [
    ":display_gfx",
    ":display_gralloc",
    "//third_party/libdrm:libdrm",
  ]
  cflags_cc = [
    "-Wno-error=unused-function",
    "-Wno-error=missing-braces",
    "-Wno-error=#warnings",
  ]
  external_deps = [
    "c_utils:utils",
    "hiviewdfx_hilog_native:libhilog",
  ]

  symlink_target_name = [ "libdisplay_layer.z.so" ]

  install_enable = true
  install_images = [ chipset_base_dir ]
  subsystem_name = "hdf"
  part_name = "drivers_peripheral_display"
}

group("display_layer") {
  deps = [
    ":display_device",
    ":display_gfx",
    ":display_gralloc",
    ":higbm",
  ]
}
